#!/bin/zsh
# Use vim cli mode
bindkey -v
bindkey '^P' up-history
bindkey '^N' down-history

# Backspace and ^h working even after returning from command mode
bindkey '^?' backward-delete-char
bindkey '^H' backward-delete-char

# ctrl-w and ctrl-backspace delete the previous word in insert mode.
bindkey '^W'  backward-kill-word
#bindkey '^\b' backward-kill-word

# ctrl-r starts searching history backward
bindkey '^R' history-incremental-search-backward

# Make arrow keys and jk search history
bindkey -a 'j' down-line-or-search
bindkey -a 'k' up-line-or-search
bindkey -a "^[[B" down-line-or-search
bindkey -a "^[[A" up-line-or-search
bindkey "^[[B" down-line-or-search
bindkey "^[[A" up-line-or-search

# Toggle stopping programs with ^Z
fancy-ctrl-z () {
  if [[ $#BUFFER -eq 0 ]]; then
    BUFFER="fg"
    zle accept-line -w
  else
    zle push-input -w
    zle clear-screen -w
  fi
}
zle -N fancy-ctrl-z
bindkey '^Z' fancy-ctrl-z
bindkey -a '^Z' fancy-ctrl-z

# Aliases
alias cb='popd 1> /dev/null' # silent popd
alias :q=exit
alias hibernate='sudo systemctl hibernate'
alias cal='cal -m' # cal with monday as the week's first day
alias bc='bc -lq' # bc with floats, without header
alias -g dd='dd status=progress'
alias rm='rm -I'
alias vim="$VISUAL"

function dotfiles() {
    git --git-dir="$HOME/.dotfiles/" --work-tree="$HOME" $@
}

# mkdir and cd
function mcdir ()
{
    mkdir -p -- "$1" &&
    cd -P -- "$1"
}

# Syntax highlighting, autosuggestions, and fzf
plugins=(
    /usr/share/zsh/plugins/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh
    /usr/share/zsh/plugins/zsh-autosuggestions/zsh-autosuggestions.zsh
    /usr/share/fzf/key-bindings.zsh
    /usr/share/fzf/completion.zsh
)
for plugin in $plugins; do
    [ -f "$plugin" ] && source "$plugin" || echo "Missing $plugin"
done

# Better command not found hook
# command_not_found_handler() {
#         RANDOM=$(date "+%N")
#         catimg "$HOME/.thonk/$((RANDOM % $(ls -l $HOME/.thonk/ | egrep -c '^-'))).png" -w 100
#         printf 'zsh: command not found: %s\n' "$1" 1>&2
#         return 127
# }

# Base16 Shell
BASE16_SHELL="$HOME/.config/base16-shell/"
[ -n "$PS1" ] && \
    [ -s "$BASE16_SHELL/profile_helper.sh" ] && \
        eval "$("$BASE16_SHELL/profile_helper.sh")"

# Function to add mode to the right prompt
function zle-line-init zle-keymap-select {
    RPS1="${${KEYMAP/vicmd/%B%F{9\}NORMAL%f%b}/(main|viins)/%F{12\}INSERT%f}"
    # RPS2="$RPS1"
    zle reset-prompt
}
zle -N zle-line-init
zle -N zle-keymap-select
